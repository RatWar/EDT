
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СтрокаЗапроса = ТекстЗапроса();
	ТЗ = ОИМЦ_Данные.ЗапросДанных171(СтрокаЗапроса);	
	Если ТЗ <> Неопределено Тогда
		Настройки = КомпоновщикНастроек.ПолучитьНастройки(); 
		ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; 
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;	
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);	
		ВнешнийНаборДанных = Новый Структура("ВнешняяТаблица", ТЗ); //Внешний набор данных записываем в структуру, где ключ = имени внешнего набора данных в СКД 
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки); //Устанавливаем в СКД внешний набор данных
		ДокументРезультат.Очистить();
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент; 
		ПроцессорВывода.УстановитьДокумент(ДокументРезультат); 
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);	
	КонецЕсли;
КонецПроцедуры

Функция ТекстЗапроса()
	Возврат 
	"select
	|	pg_database.datname as database_name,
	|   pg_database_size(pg_database.datname) / 1048576 as size,
	|	pg_size_pretty(pg_database_size(pg_database.datname)) as size_p
	|from
	|	pg_database
	|order by
	|	pg_database_size(pg_database.datname) desc;";   
КонецФункции
 